buildscript {
	repositories {
		maven { url = "http://jcenter.bintray.com" }
	}
	
	dependencies {
		classpath "us.carrclan.david.gradle:gradle-site-plugin:0.1.2"
	}
}

allprojects {
	ext {
		javaVersion = 1.8
		
		libgdxVersion = "1.1.1-SNAPSHOT"
		
		libVersion = file("$rootProject.projectDir/VERSION").text
		libName = "libgdx-utils"
		libManifestAttributes = [
			"web": "http://dermetfan.net",
			"bitbucket": "http://bitbucket.org/dermetfan/libgdx-utils",
			"bintray": "http://bintray.com/dermetfan/generic/libgdx-utils",
			"version": libVersion
		]
		
		nameAppendix = project != rootProject ? project.name : ""
		nameFull = libName + (nameAppendix.empty ? "" : "-$nameAppendix")
	}
	
	repositories {
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	}
	
	apply plugin: "java"
	apply plugin: "idea"
	apply plugin: "eclipse"
	apply plugin: "site"
	apply plugin: "announce"
	
	dependencies {
		testCompile "junit:junit:+"
	}
	
	// java
	
	sourceSets {
		main {
			java.srcDir "src/main"
			resources.srcDir "src/main"
		}
		test.java.srcDir "src/test"
	}
	
	sourceCompatibility = javaVersion
	
	jar {
		baseName = nameFull
		manifest.attributes += libManifestAttributes
	}
	
	javadoc {
		title = nameFull
		options {
			memberLevel = JavadocMemberLevel.PACKAGE
			author true
			setUse true
		}
	}
	
	// IDE integration
	
	eclipse {
		project.name = nameFull
		classpath {
			sourceSets -= sourceSets.site
			containers = ["org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-$javaVersion"]
		}
	}
	
	// distribution
	
	task distSources(type: Jar, group: "distribution") {
		description = "Assembles a jar archive containing the main sources and resources."
		from sourceSets.main.java
		from sourceSets.main.resources
		baseName = nameFull
		appendix = "sources"
		destinationDir = jar.destinationDir
		manifest.attributes += libManifestAttributes
		doLast { announce.local.send "$nameFull $libVersion", "distributed sources" }
	}
	
	task distJavadoc(type: Jar, dependsOn: javadoc, group: "distribution") {
		description = "Assembles a jar archive containing the javadoc."
		from javadoc.destinationDir
		baseName = nameFull
		appendix = "javadoc"
		destinationDir = jar.destinationDir
		manifest.attributes += libManifestAttributes
		doLast { announce.local.send "$nameFull $libVersion", "distributed javadoc" }
	}

	task distBundle(type: Zip, dependsOn: [build, distSources, distJavadoc], group: "distribution") {
		description = "Assembles a zip archive containing the binary, source and javadoc jar archives as well as the LICENSE file."
		from distSources.destinationDir
		from distJavadoc.destinationDir
		from "$rootDir/LICENSE"
		destinationDir = temporaryDir
		baseName = nameFull
		doLast {
			archivePath.renameTo("$jar.destinationDir.path/$archiveName")
			announce.local.send "$nameFull $libVersion", "distributed bundle"
		}
	}

	task cleanDistBundle(type: Delete, group: "distribution") {
		description = "Deletes the artifacts of distBundle."
		delete "$jar.destinationDir.path/${nameFull}.zip"
	}

	// publish
	
	sourceSets.site.resources {
		srcDir javadoc.destinationDir.parentFile
		srcDir testReportDir.parentFile
	}
	
	site {
		url = "ftp://dermetfan.net/$libName${nameAppendix.empty ? '' : '/' + nameAppendix}"
		authenticationInfo {
			userName = site_ftp_username
			password = site_ftp_password
		}
	}
	
	publishSite {
		dependsOn javadoc
		doFirst {
			baseDir.mkdirs() // workaround issue #2 https://bitbucket.org/davidmc24/gradle-site-plugin/issue/2/publishsite-basedir-is-not-automatically
			announce.local.send "$nameFull $libVersion", "Publishing Javadoc to ${url}..."
		}
		doLast { announce.local.send "$nameFull $libVersion", "Published Javadoc $libVersion to $url" }
		group = "publish"
	}
	
	task publishBintray(type: Exec, group: "publish") {
		description = "Publishes all artifacts in the jar task destination directory to Bintray."
		executable "$rootProject.projectDir/bintray"
		args jar.destinationDir, libVersion, bintray_api_key
	}
	
	task publish(dependsOn: [publishBintray, publishSite], group: "publish") {
		description = "Meta task for all publish tasks."
	}
}

dependencies {
	compile "com.badlogicgames.gdx:gdx:$libgdxVersion"
}

project(":box2d") {
	dependencies {
		compile rootProject
		compile "com.badlogicgames.gdx:gdx-box2d:$libgdxVersion"
	}
}
