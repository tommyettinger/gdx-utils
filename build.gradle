allprojects {
	ext {
		javaVersion = 1.7
		libVersion = 215
		libName = "libgdx-utils"
		libManifestAttributes = [
			"web": "http://dermetfan.net/",
			"repository": "http://bitbucket.org/dermetfan/libgdx-utils",
			"version": libVersion
		]
		displayName = libName + (project != rootProject ? "-$project.name" : "")
	}
	
	apply plugin: "java"
	apply plugin: "eclipse"
	apply plugin: "announce"
	
	sourceSets {
		main {
			java.srcDir "src/main"
			resources.srcDir "src/main"
		}
		test.java.srcDir "src/test"
	}
	
	sourceCompatibility = javaVersion
	
	jar {
		baseName = displayName
		manifest.attributes += libManifestAttributes
	}

	javadoc.title = displayName

	eclipse {
		project.name = displayName
		classpath.containers = ["org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-$javaVersion"]
	}
	
	dependencies {
		compile "com.badlogicgames.gdx:gdx:1.0-SNAPSHOT"
		testCompile "junit:junit:4.11"
	}
	
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	}
	
	task distSources(type: Jar) {
		from sourceSets.main.java
		from sourceSets.main.resources
		baseName = displayName
		appendix = "sources"
		destinationDir = jar.destinationDir
		manifest.attributes += libManifestAttributes
		doLast { announce.local.send(displayName, "v$libVersion sources distributed") }
	}
	
	task distDocs(type: Jar, dependsOn: javadoc) {
		from javadoc.destinationDir
		baseName = displayName
		appendix = "javadoc"
		destinationDir = jar.destinationDir
		manifest.attributes += libManifestAttributes
		doLast { announce.local.send(displayName, "v$libVersion docs distributed") }
	}
	
	task dist(type: Zip, dependsOn: [build, distSources, distDocs]) {
		from distSources.destinationDir
		from distDocs.destinationDir
		from "$rootDir/LICENSE"
		from "$rootDir/RELEASE NOTES"
		destinationDir = file "build/tmp/$name"
		baseName = libName
		doLast {
			archivePath.renameTo("$jar.destinationDir.path/$archiveName")
			announce.local.send(libName, "v$libVersion distributed")
		}
	}
}

project(":box2d") {
	dependencies {
		compile project(":")
		compile "com.badlogicgames.gdx:gdx-box2d:1.0-SNAPSHOT"
	}
}