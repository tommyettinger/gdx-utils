apply plugin: "java"
apply plugin: "eclipse"

ext {
	javaVersion = 1.6
	libVersion = 197
	libName = "libgdx-utils"
	libManifestAttributes = [
		"web": "http://dermetfan.net/",
		"repository": "http://bitbucket.org/dermetfan/libgdx-utils",
		"version": libVersion
	]
}

sourceCompatibility = javaVersion

sourceSets.main.java.srcDirs = ["src/main"]
sourceSets.test.java.srcDirs = ["src/test"]

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
	compile "com.badlogicgames.gdx:gdx:1.0-SNAPSHOT"
	testCompile "junit:junit:4.11"
}

jar.manifest.attributes += libManifestAttributes
jar.dependsOn check

javadoc.title = libName

eclipse.classpath.containers = ["org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-$javaVersion"]

void distConfig(Zip task) {
	task.destinationDir = file "build/distributions"
	task.baseName = libName
	if(task instanceof Jar)
		task.manifest.attributes += libManifestAttributes
}

task distDocs(type: Jar, dependsOn: javadoc) {
	distConfig tasks.distDocs
	from "build/docs/javadoc"
	appendix = "javadoc"
}

task distSources(type: Jar) {
	distConfig tasks.distSources
	from sourceSets.main.java
	appendix = "sources"
}

task dist(type: Zip, dependsOn: [jar, distDocs, distSources]) {
	distConfig tasks.dist
	from "build/distributions/$baseName-javadoc.jar"
	from "build/distributions/$baseName-sources.jar"
	from "build/libs/${baseName}.jar"
	from "RELEASE NOTES"
}
