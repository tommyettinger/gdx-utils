buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
	}

}

group 'com.github.tommyettinger'
version '0.13.7'

apply plugin: 'java-library'
apply plugin: 'com.bmuschko.nexus'

ext {
	libName = project.name
	nameAppendix = ''
	nameFull = libName
	libgdxVersion = '1.9.12'
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
	jcenter()
}

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			// The -quiet is because of some sort of weird JDK JavaCompiler bug:
			// https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
			options.addStringOption('Xdoclint:all,-missing', '-quiet')
		}
	}
}

javadoc.destinationDir = file("../docs/$libName/apidocs")

sourceCompatibility = 1.7

sourceSets {
	main.java.srcDirs = ['src/main']
	main.resources.srcDirs = ['src/main']
	test.java.srcDirs = ['src/test']
}

jar {
	archiveBaseName.set(libName)
	archiveAppendix.set(nameAppendix)
	manifest {
		attributes 'Implementation-Title': libName, 'Implementation-Version': archiveVersion
	}
}

dependencies {
	api "com.badlogicgames.gdx:gdx:$libgdxVersion"
	testImplementation 'junit:junit:4.12'
}

modifyPom {
	project {
		name 'libgdx-utils'
		description "An updated fork of dermetfan's famed library for libGDX convenience."
		url 'https://github.com/tommyettinger/gdx-utils'
		inceptionYear '2019'

		scm {
			url 'https://github.com/tommyettinger/gdx-utils'
			connection 'scm:https://tommyettinger@github.com/tommyettinger/gdx-utils.git'
			developerConnection 'scm:git://github.com/tommyettinger/gdx-utils.git'
		}

		licenses {
			license {
				name 'Apache License 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id 'tommyettinger'
				name 'Tommy Ettinger'
				email 'tommy.ettinger@gmail.com'
			}
		}
	}
}
